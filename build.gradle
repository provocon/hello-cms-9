/*
 * Copyright 2017 Martin Goellnitz for PROVOCON.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'distribution'
apply plugin: 'java-base'

// Let local be command line switchable
def locale = (project.hasProperty('locale') ? "$locale" : 'de-DE').toString()
// Set base pah below /Sites
def contentpath = (project.hasProperty('contentpath') ? "$contentpath" : 'HelloWorld/Germany/German').toString()
// Set title for this size
def title = (project.hasProperty('title') ? "$title" : 'Hello World').toString()

// Optionally override the short title
def shorttitle = (project.hasProperty('shorttitle') ? "$shorttitle" : title.replace(" ", "")).toString()

def rootsegment=shorttitle.toLowerCase()
def sitemapping=(project.hasProperty('sitemapping') ? "$sitemapping" : rootsegment).toString()

def uuid="$contentpath:$locale:$shorttitle"
def siteid=""+(uuid.hashCode()<0?-uuid.hashCode():uuid.hashCode())

distZip.doFirst {
  println ""
  println "Creating content set $title with a short title of $shorttitle, a URL root segment of $rootsegment"
  println "  for the local $locale in path /Sites/${contentpath}."
  println "  The URL mapping is done via ${sitemapping}."
  println "  The site id of $siteid is derived from the values ${uuid}."
  println ""
  println "  Any of these values can be overridden."
  println ""
}

// Just use a ZIP to transfer the content
distTar.enabled = false

sourceSets {
  main {
    resources {
      srcDir 'src/main'
    }
  }
}

distributions {
  main {
    baseName = "helloWorld"
    contents {
      into ('') {
        from 'src/main'
        include '**/*.xml'
        filter(ReplaceTokens, tokens: [locale: locale, contentpath: contentpath, shorttitle: shorttitle, sitetitle: title, rootsegment: rootsegment, siteid: siteid, sitemapping: sitemapping])
      }
      into ('') {
        from 'src/main'
        exclude '**/*.xml'
      }
    }
  }
}
